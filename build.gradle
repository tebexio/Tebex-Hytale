plugins {
    id 'java'
}

group = 'io.tebex'
version = '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
    // Local hytale server jar in libs folder
    implementation files('libs/HytaleServer.jar')

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def generatedJavaDir = layout.buildDirectory.dir("filtered-src")
tasks.register('filterJavaVersion', Copy) {
    from 'src/main/java'
    into generatedJavaDir

    // Replace the {VERSION} token in all Java files
    filter { String line ->
        line.replace('{{VERSION}}', project.version.toString())
    }
}

compileJava {
    dependsOn tasks.named('filterJavaVersion')
    source = fileTree(generatedJavaDir)
}

processResources {
    filesMatching('**/manifest.json') {
        expand(
                VERSION: project.version
        )
    }
}